-- CREATE TABLE
CREATE TABLE ENBDBA_ALTER_USER_AUDIT_LOG
(
  AUDIT_DATE           DATE,
  USER_ID              NUMBER,
  IP_ADDRESS           VARCHAR2(20),
  HOST                 VARCHAR2(100),
  TERMINAL             VARCHAR2(100),
  OS_USER              VARCHAR2(100),
  MODULE               VARCHAR2(100),
  SYS_EVENT            VARCHAR2(20),
  USERNAME             VARCHAR2(30),
  ACCOUNT_STATUS       VARCHAR2(30),
  LOCK_DATE            DATE,
  PASSWORD_DATE        DATE,
  DEFAULT_TABLESPACE   VARCHAR2(30),
  TEMPORARY_TABLESPACE VARCHAR2(30),
  PROFILE              VARCHAR2(30),
  SQL_TEXT             VARCHAR2(4000)
)
TABLESPACE AUDIT_ARCHIVE;

-- CREATE TRIGGER
CREATE OR REPLACE TRIGGER ENBDBA_AFTER_ALTER_USER
  AFTER ALTER ON DATABASE
DECLARE
  SQL_TEXT            ORA_NAME_LIST_T;
  SQL_TEXT_FINAL      VARCHAR2(1000);
  ACCOUNT_STATUS_TEMP CHAR(1);
  N                   NUMBER;
BEGIN
  N              := ORA_SQL_TXT(SQL_TEXT);
  SQL_TEXT_FINAL := '';
  FOR I IN 1 .. N LOOP
    SQL_TEXT_FINAL := SQL_TEXT_FINAL || UPPER(SQL_TEXT(I));
  END LOOP;
  ACCOUNT_STATUS_TEMP := '';
  IF (INSTR(SQL_TEXT_FINAL, 'ACCOUNT') > 0) THEN
    ACCOUNT_STATUS_TEMP := SUBSTR(REPLACE(SQL_TEXT_FINAL, ' ', ''),INSTR(REPLACE(SQL_TEXT_FINAL, ' ', ''),'ACCOUNT') + 7,1);
  END IF;
  IF (ORA_DICT_OBJ_TYPE = 'USER') THEN
    INSERT INTO SYS.ENBDBA_ALTER_USER_AUDIT_LOG
      SELECT SYSDATE,
             SYS_CONTEXT('USERENV', 'SESSION_USERID'),
             SYS_CONTEXT('USERENV', 'IP_ADDRESS'),
             SYS_CONTEXT('USERENV', 'HOST'),
             SYS_CONTEXT('USERENV', 'TERMINAL'),
             SYS_CONTEXT('USERENV', 'OS_USER'),
             SYS_CONTEXT('USERENV', 'MODULE'),
             ORA_SYSEVENT,
             ORA_DICT_OBJ_NAME,
             DECODE(ACCOUNT_STATUS_TEMP,'L','LOCKED','U','OPEN',A.ACCOUNT_STATUS),
             DECODE(ACCOUNT_STATUS_TEMP,'L',SYSDATE,'U',NULL,A.LOCK_DATE),
             B.PTIME,
             A.DEFAULT_TABLESPACE,
             A.TEMPORARY_TABLESPACE,
             A.PROFILE,
             UPPER(SQL_TEXT_FINAL)
        FROM DBA_USERS A, USER$ B
       WHERE A.USERNAME = ORA_DICT_OBJ_NAME
         AND A.USERNAME = B.NAME;
  END IF;
END;
